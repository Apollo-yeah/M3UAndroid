plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "com.google.dagger.hilt.android"
    id "kotlin-kapt"
}

android {
    signingConfigs {
        release {
            storeFile file(project.rootProject.file("release.jks"))
            Properties properties = new Properties()
            properties.load(project.rootProject.file("local.properties").newDataInputStream())

            storePassword properties.getProperty("release.storePassword")
            keyAlias properties.getProperty("release.keyAlias")
            keyPassword properties.getProperty("release.keyPassword")
        }
    }
    namespace "com.m3u.app"
    compileSdk 33
    defaultConfig {
        applicationId "com.m3u.app"
        minSdk 26
        targetSdk 33
        versionCode 37
        versionName "1.11.0-beta01"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }

        unminify {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = rootProject.ext.compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation project(":core")
    implementation project(":ui")
    implementation project(":features:main")
    implementation project(":features:favorite")
    implementation project(":features:setting")
    implementation project(":features:feed")
    implementation project(":features:live")
    implementation project(":features:console")
    implementation project(":features:crash")

    implementation libs.androidx.core.core.ktx
    implementation libs.androidx.appcompat.appcompat
    implementation libs.androidx.activity.activity.compose

    implementation libs.androidx.lifecycle.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.lifecycle.runtime.compose

    implementation libs.androidx.core.core.splashscreen

    implementation libs.com.google.dagger.hilt.android
    kapt libs.com.google.dagger.hilt.compiler
    implementation libs.androidx.hilt.hilt.navigation.compose
}